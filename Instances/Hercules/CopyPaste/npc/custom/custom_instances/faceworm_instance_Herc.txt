//Original by Emistry
//Rewritten to work on Hercules by Aeromesi

function	script	epoch_time	{
		.@time = getarg(0);
		.@hour = ( .@time / 3600 );
		.@min = ( .@time % 3600 ) / 60 ;
		.@sec = ( ( .@time % 3600 ) % 60 ) % 60;
		
		return sprintf( "%02d:%02d:%02d",.@hour,.@min,.@sec );
	}

		
function	script	npc_talk	{
		.@argcount = getargcount();
		.@npc_name$ = strnpcinfo(1);
		
		for ( .@i = 0; .@i < .@argcount; .@i++ ) {
			mes "^0055FF[ "+.@npc_name$+" ]^000000";
			.@message$ = getarg( .@i );
			mes .@message$;
			npctalk .@message$;
			next;
		}
		return;
	}




// instance
1@face	mapflag	src4instance
1@face	mapflag	zone	Memorial Dungeon
1@face	mapflag	nocommand	10
1@face	mapflag	nosave

pay_fild09,115,26,3	script	Faceworm Expedition#vkro	4_M_EIN_SOLDIER,{
	doevent "faceworm_nest::OnTalk";
}
// pay_arche,49,133,4	duplicate(Faceworm Expedition#vkro)	Faceworm Expedition#1	4_M_EIN_SOLDIER


-	script	faceworm_nest	4_M_EIN_SOLDIER,{
OnTalk:
	.@party_id = getcharid(1);
	.@is_leader = ( getcharid(0) == getpartyleader(.@party_id,2) );
	.@has_instance = ( has_instance( .map$[0] ) != "" );
	
	mes "^0055FF[ "+.instance_name$+" ]^000000";
	mes "Recently, I received several report on somebody spotted Faceworm appeared at Payon Field and start attacking adventures.";
	mes "It's dangerous wildlife, we should take it down before it harm even more adventures.";
	mes "Would you like to join the expedition with us? We're looking for strong people like you.";
	next;
	mes "^0055FF[ "+.instance_name$+" ]^000000";
	// if ( getgmlevel() < 99 ) {
		// mes "It's under maintenance, not available for short time.";
		// close;
	// }
	setarray .@party_member,1,12;
	mes "If you wish to join us...";
	mes "Form a party with "+.@party_member[0]+" ~ "+.@party_member[1]+" members.";
	if ( !.@party_id ) close;
	// mes " ^FF00EE��������^000000��  80%]^000000";
	next;
	switch( select( 
		( .@party_id && .@is_leader && !.@has_instance )?"Join Expedition":"",
		( 0 && .@party_id && .@is_leader && .@has_instance )?"":"", // Enter Memorial Dungeon
		( .@party_id && .@is_leader && .@has_instance )?"Give Up":"",
		"Leave"
	)){
		case 1:
			getpartymember .@party_id,1;
			getpartymember .@party_id,2;
		
			.@origin = getcharid(3);
			.@time = gettimetick(2);
			for ( .@i = 0; .@i < $@partymembercount; .@i++ )
				if ( isloggedin( $@partymemberaid[.@i],$@partymembercid[.@i] ) ) 
					if ( attachrid( $@partymemberaid[.@i] ) ) {
						.@member_count++;
						
						if ( .delay_timer && .@time < instance_faceworm ) {
							.@fail_type |= 1;
							.@cooldown = ( instance_faceworm - .@time );
						}
						if ( Zeny < .zeny_cost ) .@fail_type |= 2;
						
						if ( .@fail_type ) {
							.@name$ = strcharinfo(0);
							break;
						}
					}
			attachrid( .@origin );
			
			if( .@name$ != "" ){
				next;
				mes "^0055FF[ "+.instance_name$+" ]^000000";
				mes "^0055FF"+.@name$+"^000000";
				if ( .@fail_type & 1 ) mes "cooldown left "+epoch_time( .@cooldown );
				if ( .@fail_type & 2 ) mes "not enough zeny.";
				break;
			}
			
			if( .@member_count < .@party_member[0] || .@member_count > .@party_member[1] ){
				break;
			}
			else {
				.@instance = instance_create( .instance_name$,.@party_id );
				if( .@instance < 0 ) {
					npctalk "Memorial Dungeon reservation failed.";
					mes "^0000FF"+.instance_name$+" ^000000- Reservation Failed!";
					close;
				}
				mes "^0000FF"+.instance_name$+" ^000000 - Reserved";
				for( .@i = 0; .@i < .map_size; .@i++ )
					if( instance_attachmap( .map$[.@i],.@instance ) == "" ){
						npctalk "Memorial Dungeon reservation failed.";
						mes "- ^FF0000Reservation Failed!^000000";
						instance_destroy( .@instance );
						close;
					}
		
				npctalk "Memorial Dungeon has been generated for Party - "+getpartyname( .@party_id );
				instance_set_timeout .time_limit[0],.time_limit[1],.@instance;
				instance_init( .@instance );
				
				getpartymember .@party_id,2;
				for( .@i = 0; .@i < $@partymembercount; .@i++ )
					if( attachrid( $@partymemberaid[.@i] ) ) {
						Zeny -= .zeny_cost;
						instance_faceworm = gettimetick(2) + .delay_timer;
						warp .map$[0],112,369;
					}
				break;
			}
		case 2:
			if( has_instance( .map$[0] ) == "" ){
				npctalk "Memorial Dungeon doesnt exist for Party - "+getpartyname( .@party_id );
			}
			else{
			
				getpartymember .@party_id,1;
				getpartymember .@party_id,2;
				for( .@i = 0; .@i < $@partymembercount; .@i++ )
					if ( isloggedin( $@partymemberaid[.@i],$@partymembercid[.@i] ) ) 
						if( attachrid( $@partymemberaid[.@i] ) ) {
							instance_faceworm = gettimetick(2) + .delay_timer;
							warp .map$[0],112,369;
						}
				detachrid;
			}
			break;
		case 3:
			mes "Memorial Dungeon Destroyed.";
			instance_destroy has_instance2( .map$[0] );
			break;
		default: 
			// mes "This is a Memorial Dungeon.";
			break;
	}
	close;

	OnInit:
		// delay in second
		.delay_timer = ( 3600 * 24 );
		// .zeny cost
		// .zeny_cost = 1000000;
		// name of Instance
		.instance_name$ = "Faceworm Nest";
		// instance timeout limit
		setarray .time_limit,3600,5;
		// instance map lists
		setarray .map$,"1@face";

		.map_size = getarraysize( .map$ );
		end;
}


1@face,150,92,0	warp	faceworm_instance_warp_1	2,5,1@face,128,87
1@face,139,99,0	warp	faceworm_instance_warp_2	2,5,1@face,151,108
1@face,248,186,0	warp	faceworm_instance_warp_3	2,5,1@face,260,174
1@face,204,122,0	warp	faceworm_instance_warp_4	2,5,1@face,208,146

1@face,214,165,4	script	Transport Device#fexit	PORTAL,{
	warp "SavePoint",0,0;
	end;
}


1@face,1,1,4	script	faceworm_nest_instance	-1,{
	function party_delay;

	function	party_delay	{
		.@map$ = getarg( 0 );
		.@party_id = getarg( 1 );
		.@time = gettimetick(2) + getvariableofnpc( .delay_timer, "faceworm_nest" );
		
		getpartymember .@party_id,2;
		for( .@i = 0; .@i < $@partymembercount; .@i++ )
			if( attachrid( $@partymemberaid[.@i] ) )
				if ( strcharinfo(3) == .@map$ )
					instance_faceworm = .@time;
		detachrid;
	}
	
	OnInstanceInit:
		'regularWorm = 2528;
		'darkWorm = 2530;
		sleep 500;
		.@npc_name$ = strnpcinfo(3);
		'ladder_level = 0;
		'talking = 1;
		
		donpcevent instance_npcname( "Chaos#faceworm" )+"::OnEnable";
		donpcevent instance_npcname( "Iris#faceworm" )+"::OnEnable";
		
		// warp portal
		for ( .@i = 1; .@i <= 4; .@i++ )
			disablenpc instance_npcname( "faceworm_instance_warp_"+.@i );
		disablenpc instance_npcname("Transport Device#fexit");
		end;
		
	OnStart1:
		.@map$ = "1@face";
		.@map_user = getmapusers( .@map$ );
		.@npc_name$ = instance_npcname( "Treasure#faceworm" );
		if ( .@map_user > 1 )
			.@extra = ( ( 11 * .@map_user ) / 5  );
		monster .@map$,84,331,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd1";
		monster .@map$,72,288,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd1";
		monster .@map$,73,215,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd1";
		monster .@map$,70,131,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd1";
		monster .@map$,138,67,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd1";
		monster .@map$,138,67,"--ja--",'darkWorm,1,.@npc_name$+"::OnEnd1";
		initnpctimer .@npc_name$;
		end;
		
	OnStart2:
		.@map$ = strnpcinfo(4);
		.@map_user = getmapusers( .@map$ );
		.@npc_name$ = instance_npcname( "Treasure#faceworm" );
		if ( .@map_user > 1 )
			.@extra = ( ( 11 * .@map_user ) / 4  );
		monster .@map$,102,123,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd2";
		monster .@map$,112,158,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd2";
		monster .@map$,124,244,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd2";
		monster .@map$,167,271,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd2";
		monster .@map$,167,271,"--ja--",'darkWorm,1,.@npc_name$+"::OnEnd2";
		initnpctimer .@npc_name$;
		end;
		
	OnStart3:
		.@map$ = strnpcinfo(4);
		.@map_user = getmapusers( .@map$ );
		.@npc_name$ = instance_npcname( "Treasure#faceworm" );
		.@extra = ( ( 11 * .@map_user ) / 4  );
		monster .@map$,161,132,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd3";
		monster .@map$,177,194,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd3";
		monster .@map$,271,315,"--ja--",'darkWorm,1,.@npc_name$+"::OnEnd3";
		initnpctimer .@npc_name$;
		end;
		
	OnStart4:
		.@map$ = strnpcinfo(4);
		.@map_user = getmapusers( .@map$ );
		.@npc_name$ = instance_npcname( "Treasure#faceworm" );
		if ( .@map_user > 1 )
			.@extra = ( ( 11 * .@map_user ) / 3  );

		monster .@map$,267,115,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd4";
		monster .@map$,252,121,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd4";
		monster .@map$,215,110,"--ja--",'regularWorm,( 4 + .@extra ),.@npc_name$+"::OnEnd4";
		monster .@map$,213,106,"--ja--",'darkWorm,1,.@npc_name$+"::OnEnd4";
		initnpctimer .@npc_name$;
		end;
		
	OnStart5:
		.@map$ = strnpcinfo(4);
		.@map_user = getmapusers( .@map$ );
		.@npc_name$ = instance_npcname( "Treasure#faceworm" );
		
		sleep 3000;
		mapannounce .@map$,"Something huge is approaching inside the Cave!!",bc_map;
		sleep 3000;
		mapannounce .@map$,"This doesn't look good!! Run now while you still have the chances.",bc_map;
		
		'type = 0;
		'boss_gid = monster( .@map$,213,156,"--ja--",2529,1,.@npc_name$+"::OnEnd5" );
		.@hp = ( 26000000 + ( 2000000 * getmapusers( .@map$ ) ) );
		setunitdata( 'boss_gid,UDT_HP,.@hp  );
		mapannounce .@map$,"Faceworm Queen has appeared!!",bc_map;
		initnpctimer .@npc_name$;
		initnpctimer;
		end;	
	
	OnTimer20000:
	OnTimer40000:
		areamonster strnpcinfo(4),196,144,234,172,"--ja--",2540,50;
		end;
	OnTimer10000:
	OnTimer30000:
	OnTimer50000:
		areamonster strnpcinfo(4),196,144,234,172,"--ja--",2531,50;
		end;
	
	OnTimer60000:
		.@map$ = strnpcinfo(4);
		.@npc_name$ = instance_npcname( "Treasure#faceworm" );
		
		.@max_hp = getmonsterinfo( XM_CELINE_KIMI,MOB_MAXHP );
		.@hp = getunitdata( 'boss_gid,UDT_HP );
//		.@x = getunitdata( 'boss_gid,6 );
//		.@y = getunitdata( 'boss_gid,7 );
		.@x = 213;
		.@y = 156;
		
		.@element = rand( 2532,2535 );
		killmonsterall .@map$,.@npc_name$;
//		mapannounce .@map$,"The Faceworm Queen has shed its skin! Its new element is "+.@element+"! Be careful!",bc_map;
		'boss_gid = monster( .@map$,.@x,.@y,"--ja--",.@element,1,.@npc_name$+"::OnEnd5" );
		setunitdata( 'boss_gid,UDT_HP,.@hp  );
		initnpctimer;
		
		switch ( .@element ) {
			case FACEWORM_QUEEN_R:
				mapannounce .@map$,"Faceworm Queen changed to Fire element!",bc_map;
				break;
			case FACEWORM_QUEEN_G:
				mapannounce .@map$,"Faceworm Queen changed to Earth element!",bc_map;
				break;
			case FACEWORM_QUEEN_B:
				mapannounce .@map$,"Faceworm Queen changed to Water element!",bc_map;
				break;
			case FACEWORM_QUEEN_Y:
				mapannounce .@map$,"Faceworm Queen changed to Wind element!",bc_map;
				break;
		}
		end;
		
}


1@face,161,132,4	script	#faceworm_trap	-1,5,5,{
	OnTouch_:
		.@size = 5;
		getmapxy( .@map$,.@x,.@y,1 );
		.@x1 = .@x - .@size;
		.@y1 = .@y - .@size;
		.@x2 = .@x + .@size;
		.@y2 = .@y + .@size;
		areamonster .@map$,.@x1,.@y1,.@x2,.@y2,"--ja--",2531,10;
		end;
}

1@face,167,148,4	duplicate(#faceworm_trap)	#faceworm_trap_01	-1,5,5
1@face,165,165,4	duplicate(#faceworm_trap)	#faceworm_trap_02	-1,5,5
1@face,177,189,4	duplicate(#faceworm_trap)	#faceworm_trap_03	-1,5,5
1@face,181,198,4	duplicate(#faceworm_trap)	#faceworm_trap_04	-1,5,5
1@face,195,200,4	duplicate(#faceworm_trap)	#faceworm_trap_05	-1,5,5
1@face,205,202,4	duplicate(#faceworm_trap)	#faceworm_trap_06	-1,5,5
1@face,215,205,4	duplicate(#faceworm_trap)	#faceworm_trap_07	-1,5,5
1@face,242,225,4	duplicate(#faceworm_trap)	#faceworm_trap_08	-1,5,5
1@face,243,241,4	duplicate(#faceworm_trap)	#faceworm_trap_09	-1,5,5
1@face,242,273,4	duplicate(#faceworm_trap)	#faceworm_trap_10	-1,5,5
1@face,241,283,4	duplicate(#faceworm_trap)	#faceworm_trap_11	-1,5,5
1@face,275,316,4	duplicate(#faceworm_trap)	#faceworm_trap_12	-1,5,5
1@face,269,291,4	duplicate(#faceworm_trap)	#faceworm_trap_13	-1,5,5
1@face,269,291,4	duplicate(#faceworm_trap)	#faceworm_trap_14	-1,5,5




1@face,1,1,4	script	Treasure#faceworm	4_TREASURE_BOX,{
	
	.@party_id = getcharid(1);
	.@is_leader = ( getcharid(0) == getpartyleader(.@party_id,2) );
	
	if ( !.@is_leader || 'stage_time <= 0 ) end;
	
	switch ( 'ladder_level ) {
		case 1:
		case 2:
		case 3:
			.@timer = 121;
			.@refine = rand( 0,6 );
			setarray .@slot,0,
				rand( 4853,4858 ),
				4700 + ( rand( 6 ) * 10 ) + rand( 5 ),
				4700 + ( rand( 6 ) * 10 ) + rand( 5 );
		case 4:
			.@timer = 111;
			.@refine = rand( 0,6 );
			setarray .@slot,0,
				rand( 4853,4858 ),
				4700 + ( rand( 6 ) * 10 ) + rand( 5 ),
				4700 + ( rand( 6 ) * 10 ) + rand( 5 );
			break;
		case 5:
			.@timer = 271;
			.@refine = rand( 0,11 );
			setarray .@slot,0,
				rand( 4853,4858 ),
				4700 + ( rand( 6 ) * 10 ) + rand( 7 ),
				4700 + ( rand( 6 ) * 10 ) + rand( 7 );
		default: break;
	}
	
	.@item_id = ( ( rand(100) < 90 )? 20717:20718 );
	
	if ( 'stage_time >= .@timer ) {
		deletearray .@slot;
	}
	else {
		if ( rand(100) < 90 ) .@slot[1] = 0;
		if ( rand(100) < 50 ) .@slot[2] = 0;
		if ( rand(100) < 90 || !.@slot[2] ) .@slot[3] = 0;
	}
	
	getitem2 .@item_id,1,0,.@refine,0,.@slot[0],.@slot[1],.@slot[2],.@slot[3];
	
	message strcharinfo(0),"Reward : +"+.@refine+" "+getitemname( .@item_id );
	for ( .@i = 0; .@i < 4; .@i++ ) 
		if ( .@slot[.@i] ) 
			dispbottom " >> "+getitemname( .@slot[.@i] );
	
	OnDisable:
		.@npc_name$ = strnpcinfo(3);
		hideonnpc .@npc_name$;
		movenpc .@npc_name$,1,1;
		end;
		
	OnInit:
	OnEnable:
		.@npc_name$ = strnpcinfo(3);
		hideoffnpc .@npc_name$;
		end;
		
	OnEnableNPC:
		if ( 'ladder_level <= 4 )
			enablenpc instance_npcname( "faceworm_instance_warp_"+'ladder_level );
		donpcevent instance_npcname( "Chaos#faceworm" )+"::OnEnable";
		donpcevent instance_npcname( "Iris#faceworm" )+"::OnEnable";
		return;
	
	OnEnd1:
		.@map$ = strnpcinfo(4);
		.@npc_name$ = strnpcinfo(3);
		.@mobcount = mobcount( .@map$,.@npc_name$+"::OnEnd1" );
		if ( .@mobcount > 0 ) {
			mapannounce .@map$,"Faceworm Alive : "+.@mobcount,bc_map;
		}
		else {
			.@timer = getnpctimer(0) / 1000;
			announce "Stage 1: Completed in "+epoch_time( .@timer ),bc_map;
			'stage_time = .@timer;
			stopnpctimer;
			movenpc .@npc_name$,149,77;
			hideoffnpc .@npc_name$;
			'ladder_level++;
			callsub OnEnableNPC;
		}
		end;
		
	OnEnd2:
		.@map$ = strnpcinfo(4);
		.@npc_name$ = strnpcinfo(3);
		.@mobcount = mobcount( .@map$,.@npc_name$+"::OnEnd2" );
		if ( .@mobcount > 0 ) {
			mapannounce .@map$,"Faceworm Alive : "+.@mobcount,bc_map;
		}
		else {
			.@timer = getnpctimer(0) / 1000;
			announce "Stage 2: Completed in "+epoch_time( .@timer ),bc_map;
			'stage_time = .@timer;
			stopnpctimer;
			movenpc .@npc_name$,164,273;
			hideoffnpc .@npc_name$;
			'ladder_level++;
			callsub OnEnableNPC;
		}
		end;
		
	OnEnd3:
		.@map$ = strnpcinfo(4);
		.@npc_name$ = strnpcinfo(3);
		.@mobcount = mobcount( .@map$,.@npc_name$+"::OnEnd3" );
		if ( .@mobcount > 0 ) {
			mapannounce .@map$,"Faceworm Alive : "+.@mobcount,bc_map;
		}
		else {
			.@timer = getnpctimer(0) / 1000;
			announce "Stage 3: Completed in "+epoch_time( .@timer ),bc_map;
			'stage_time = .@timer;
			stopnpctimer;
			movenpc .@npc_name$,274,314;
			hideoffnpc .@npc_name$;
			'ladder_level++;
			callsub OnEnableNPC;
		}
		end;
		
	OnEnd4:
		.@map$ = strnpcinfo(4);
		.@npc_name$ = strnpcinfo(3);
		.@mobcount = mobcount( .@map$,.@npc_name$+"::OnEnd4" );
		if ( .@mobcount > 0 ) {
			mapannounce .@map$,"Faceworm Alive : "+.@mobcount,bc_map;
		}
		else {
			mapannounce .@map$,"I heard some weird rock cracking sound near the walls. It must be a hidden tunnel to the Hive!",bc_map;
			.@timer = getnpctimer(0) / 1000;
			announce "Stage 4: Completed in "+epoch_time( .@timer ),bc_map;
			'stage_time = .@timer;
			stopnpctimer;
			movenpc .@npc_name$,214,105;
			hideoffnpc .@npc_name$;
			'ladder_level++;
			callsub OnEnableNPC;
		}
		end;
		
	OnEnd5:
		.@map$ = strnpcinfo(4);
		.@npc_name$ = strnpcinfo(3);
		.@mobcount = mobcount( .@map$,.@npc_name$+"::OnEnd5" );
		if ( .@mobcount > 0 ) {
			mapannounce .@map$,"Faceworm Alive : "+.@mobcount,bc_map;
		}
		else {.@timer = getnpctimer(0) / 1000;
			announce "Final Stage: Completed in "+epoch_time( .@timer ),bc_map;
			'stage_time = .@timer;
			stopnpctimer instance_npcname( "faceworm_nest_instance" );
			stopnpctimer;
			sleep 1000;
			killmonster .@map$,"All";
			movenpc .@npc_name$,213,159;
			hideoffnpc .@npc_name$;
			'ladder_level++;
			sleep 3000;
			mapannounce .@map$,"This scary Faceworm nest expedition has come to an end with the defeat of the Queen!",bc_map;
			sleep 3000;
			callsub OnEnableNPC;
			sleep 2000;
			donpcevent instance_npcname( "Chaos#faceworm" )+"::OnEnd";
			donpcevent instance_npcname( "Iris#faceworm" )+"::OnEnd";
			enablenpc instance_npcname("Transport Device#fexit");
		}
		end;
}



1@face,108,351,5	script	Chaos#faceworm	4_M_CHAOS,{
		setmapflag instance_mapname( "1@face"),mf_nomvploot;
		setmapflag instance_mapname( "1@face"),mf_nomobloot;
		setmapflag instance_mapname( "1@face"),mf_zone,"Memorial Dungeon";
	
	if ( 'ladder_level == -1 ) {
		mes "You want to leave ? You wont be able to come back upon leave.";
		if ( select( "Leave","Cancel" ) == 1 ) {
			warp "Save",0,0;
		}
		close;
	}
	
	.@party_id = getcharid(1);
	.@is_leader = ( getcharid(0) == getpartyleader(.@party_id,2) );
	
	if ( !'talking || !.@is_leader ) end;
	
	cutin "h_chaos01",2;
	if ( !.@party_id ) {
		mes "^0055FF[ Faceworm Expedition ]^000000";
		mes "You dont belong to any party here.";
		close2;
		warp "Save",0,0;
		end;
	}
	
	'talking = !'talking;
	.@npc_name$ = strnpcinfo(3);
	switch ( 'ladder_level ) {
		default: 
			mes "Hmm ... ";
			close2;
			cutin "",255;
			end;
		case 0:
			npc_talk(
				"Hmm, I didnt expecting there are actually somebody else is here...",
				"The Faceworm are spotted around this area. Are you to join ?",
				"Haiz, nevermind. I wouldn't mind if you join us, but dont bring us trouble. We won't back you up here!",
				"I sensed a storm is coming soon!",
				"Iris, it seem like Faceworm is getting active now. The Queen is probably around here.", 
				"I guess both of us should take care of it as soon as possible!",
				"Let's go Iris !!"
			);
			emotion e_go;
			movenpc .@npc_name$,120,97;
			break;
		case 1:
			npc_talk( 
				"Woah ~ This is really not something we're expected.",
				"That was fast, it's great!",
				"However, it wouldn't be this easy if its the Queen.",
				"The Faceworm you seen just now, just a mere mutated Faceworm.",
				"Never underestimate it, you have seen how far it can go !",
				"It seem like no more Faceworm around here, I will go first. Iris, you deal with these adventures!"
			);
			movenpc .@npc_name$,153,114;
			break;
		case 2:
			npc_talk( 
				"I admit you are strong, but you shall be more careful starting here.",
				"The narrow valley in front are way more  dangerous..."
			);
			movenpc .@npc_name$,257,169;
			break;
		case 3:
			npc_talk( 
				"Good that you survived till now.",
				"However, our plan are seem to be working well.",
				"We're probably getting near to the Queen Hive now.",
				"Stay alert!!"
			);
			movenpc .@npc_name$,206,153;
			break;
		case 4:
			npc_talk( 
				"Hmm, this hive entrance, seem abit too huge for the Faceworm??",
				"Maybe I will just enter and take a look!"
			);
			movenpc .@npc_name$,211,157;
			break;
	}
	close2;
	hideonnpc .@npc_name$;
	cutin "",255;
	end;
	
	OnInit:
	OnDisable:
		hideonnpc strnpcinfo(3);
		end;
	
	OnEnable:
		hideoffnpc strnpcinfo(3);
		end;
		
	OnHelp:
		'talking = 1;
		npctalk "Ah!What is this!??";
		sleep 2000;
		.@message$ = "ah";
		for ( .@i = 0; .@i < 5; .@i++ ) {
			.@message$ = replacestr( .@message$,"ah","aaahhh" );
			npctalk .@message$+"...";
			emotion e_hlp;
			sleep 350;
		}
		
		donpcevent instance_npcname( "#faceworm_effect" )+"::OnEffect";
		.@npc_name$ = strnpcinfo(3);
		movenpc .@npc_name$,206,153;
		hideoffnpc .@npc_name$;
		npctalk "Damn, that was close to become the Queen Food~";
		emotion e_wah;
		sleep 3000;
		npctalk "The Queen is so angry now, and coming toward us now. I guess we shall hide ourself now.";
		emotion e_wah;
		sleep 1000;
		hideonnpc .@npc_name$;
		'talking = 0;
		donpcevent instance_npcname( "Iris#faceworm" )+"::OnHelp";
		end;
		
	OnEnd:
		npctalk "Woah, this expedition is so long...";
		sleep 3000;
		npctalk "It seem our equipments are mostly broken now, need to repair it.";
		sleep 3000;
		npctalk "I think we shall dismiss now for today!";
		sleep 3000;
		npctalk "You... you did well today!";
		sleep 2000;
		npctalk "I wont deny what you all did today, the rewards are all yours now.";
		sleep 6000;
		npctalk "Okay, let's back to the village.";
		sleep 3000;
		hideonnpc strnpcinfo(3);
		end;
}



1@face,115,348,3	script	Iris#faceworm	4_F_IRIS,{
	
	if ( 'ladder_level == -1 ) {
		mes "You want to leave ? You wont be able to come back upon leave.";
		if ( select( "Leave","Cancel" ) == 1 ) {
			warp "Save",0,0;
		}
		close;
	}
	
	.@party_id = getcharid(1);
	.@is_leader = ( getcharid(0) == getpartyleader(.@party_id,2) );
	
	if ( 'talking || !.@is_leader ) end;
	
	cutin "h_iris01",2;
	if ( !.@party_id ) {
		mes "^0055FF[ Faceworm Expedition ]^000000";
		mes "You dont belong to any party here.";
		close2;
		warp "Save",0,0;
		end;
	}
	
	'talking = !'talking;
	.@npc_name$ = strnpcinfo(3);
	switch ( 'ladder_level ) {
		default: 
			mes "Hmm ... ";
			close2;
			cutin "",255;
			end;
		case 0:
			npc_talk( 
				"Hey Chaos, dont rush there yourself!",
				"It's dangerous for you in case you met the Queen alone.",
				"Damn, he never listen to me.",
				"Sorry but I need to gather with him before he come out with any problems.",
				"Stay safe, we'll meet again young adventures."
			);
			emotion e_no1;
			movenpc .@npc_name$,127,99;
			donpcevent instance_npcname( "faceworm_nest_instance" )+"::OnStart1";
			break;
		case 1:
			npc_talk( 
				"Hey Chaos, not again??!!",
				"By the way, did you obtain the Skin after you killed it? That item is useful.",
				"But if you drag the battle too long, you could damaged the skin, end up become useless as well. Careful with that.",
				"I heard that pro hunters are able to defeat it in no time, able to carefully obtain it without damaging it.",
				"Even Chaos unable to do this. *hehe*",
				"Come, let's follow Chaos!! I am sure he need our help too."
			);
			emotion e_no1;
			movenpc .@npc_name$,159,115;
			donpcevent instance_npcname( "faceworm_nest_instance" )+"::OnStart2";
			break;
		case 2:
			npc_talk( 
				"Careful, it could have alot of traps.",
				"Look carefully around you. Stay Alert!",
				"We'll move on to check on the situation."
			);
			emotion e_no1;
			movenpc .@npc_name$,265,171;
			donpcevent instance_npcname( "faceworm_nest_instance" )+"::OnStart3";
			break;
		case 3:
			npc_talk( 
				"We're gonna eliminate all the remaining Faceworm once and for all.",
				"Fight till the very end, brave adventures."
			);
			movenpc .@npc_name$,221,153;
			donpcevent instance_npcname( "faceworm_nest_instance" )+"::OnStart4";
			break;
		case 4:
			npc_talk( 
				"Come on Chaos!! Are you kidding!!??",
				"That's way too dangerous, we dunno what could be waiting inside it."
			);
			donpcevent instance_npcname( "Chaos#faceworm" )+"::OnHelp";
			close2;
			cutin "",255;
			end;
	}
	close2;
	hideonnpc .@npc_name$;
	cutin "",255;
	end;

	OnInit:
	OnDisable:
		hideonnpc strnpcinfo(3);
		end;
	
	OnEnable:
		hideoffnpc strnpcinfo(3);
		end;
		
	OnHelp:	
	
		.@npc_name$ = strnpcinfo(3);
		npctalk "Damn, Chaos you're such a Fool!!";
		sleep 1000;
		npctalk "You should just just die inside there and become the food for Queen.";
		sleep 2000;
		npctalk "Damn it, run and save yourself adventures!";
		sleep 3000;
		hideonnpc .@npc_name$;
		donpcevent instance_npcname( "faceworm_nest_instance" )+"::OnStart5";
		end;
		
	OnEnd:
		sleep 4000;
		npctalk "But this monster doesnt seem like the one we looking for?";
		sleep 3000;
		npctalk "Although we  didnt have the chance to meet with the Faceworm King, but I am glad that nobody has died during the expedition.";
		sleep 6000;
		npctalk "Let's get back to the village , shall we ?? I am tired.";
		sleep 3000;
		hideonnpc strnpcinfo(3);
		end;
}




1@face,207,162,4	script	#faceworm_waterball_1	139,2,2,{
	end;
	// OnTouchNPC:
		// if ( 'boss_gid ) {
			// areamobuseskill "que_dance",213,157,14,'boss_gid,WZ_WATERBALL,4,1,1,e_gg,3;
		// }
		// npctalk "cast";
	end;
}


1@face,207,157,4	duplicate(#faceworm_waterball_1)	#faceworm_waterball_2	139,2,2
1@face,207,152,4	duplicate(#faceworm_waterball_1)	#faceworm_waterball_3	139,2,2
1@face,213,152,4	duplicate(#faceworm_waterball_1)	#faceworm_waterball_4	139,2,2
1@face,218,152,4	duplicate(#faceworm_waterball_1)	#faceworm_waterball_5	139,2,2
1@face,218,157,4	duplicate(#faceworm_waterball_1)	#faceworm_waterball_6	139,2,2
1@face,218,163,4	duplicate(#faceworm_waterball_1)	#faceworm_waterball_7	139,2,2
1@face,213,163,4	duplicate(#faceworm_waterball_1)	#faceworm_waterball_8	139,2,2


1@face,213,158,4	script	#faceworm_effect	139,2,2,{
	end;
	
	OnEffect:
		while ( .@i < 20 ) {
			misceffect 899;
			misceffect 901;
			.@i++;
			sleep 1000;
			misceffect 585;
		}
		end;
	

}


